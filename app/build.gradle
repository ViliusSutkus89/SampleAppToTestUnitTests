version = '1.0.3'
if (!project.hasProperty('PublicRelease')) {
//    if (System.getenv('GITHUB_SHA')) {
//        version += '-' + System.getenv('GITHUB_SHA')
//    }
    version += '-SNAPSHOT'
}
group = 'com.viliussutkus89'

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30

        versionCode 1
        versionName project.version

        archivesBaseName = rootProject.name

        consumerProguardFiles 'proguard-rules.pro'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

task sourceJar(type: Jar) {
    archiveClassifier.set('sources')
    into ("", { from android.sourceSets.main.java.srcDirs} )
}

task javadoc(type: Javadoc) {
    options.encoding = 'UTF-8'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    title = "${rootProject.name} ${project.version} API"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

publishing {
    publications {
        projectRelease(MavenPublication) {
            artifactId rootProject.name
            artifact "$buildDir/outputs/aar/${rootProject.name}-release.aar"
            artifact sourceJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/ViliusSutkus89/SampleAppToTestUnitTests"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
